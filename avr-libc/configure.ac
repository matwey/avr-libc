#                                               -*- Autoconf -*-
#
# Copyright (c) 2004,  Theodore A. Roth
# Copyright (c) 2005,  Anatoly Sokolov
# Copyright (c) 2005,  Joerg Wunsch
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# * Redistributions of source code must retain the above copyright
#   notice, this list of conditions and the following disclaimer.
# * Redistributions in binary form must reproduce the above copyright
#   notice, this list of conditions and the following disclaimer in
#   the documentation and/or other materials provided with the
#   distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.
#
# $Id$
#
# Process this file with autoconf to produce a configure script.
#

AC_PREREQ(2.57)
AC_REVISION($Revision$)
AC_INIT(avr-libc, 1.3.0.20050906, avr-libc-dev@nongnu.org)
AC_CONFIG_SRCDIR([doc/examples/demo/demo.c])
AC_CONFIG_HEADER([config.h])

AC_CANONICAL_BUILD
AC_CANONICAL_HOST

# See if the user specified '--build=`../config.guess` --host=avr' when
# running ../configure.

AC_MSG_CHECKING([if configuring for cross compile])
if test "x${build_alias}" = "x${host_alias}"; then
	if test "x${build_alias}" = "x" ; then
		build_alias='`./config.guess`'
	fi
	AC_MSG_RESULT(no)
	AC_MSG_WARN()
	AC_MSG_WARN(AVR-LIBC must be built using an avr cross-compiler.)
	AC_MSG_WARN(Try configuring with:)
	AC_MSG_WARN(\"$0 --build=$build_alias --host=avr\")
	AC_MSG_WARN()
	AC_MSG_ERROR(aborting configure)
else
	AC_MSG_RESULT([yes])
fi

AC_MSG_CHECKING([if target host is avr])
case "$host" in
	avr* )
		AC_MSG_RESULT([yes])
		;;
	* )
		AC_MSG_RESULT(no)
		AC_MSG_WARN()
		AC_MSG_WARN(Avr-libc must be built using an avr cross-compiler.)
		AC_MSG_WARN(Try configuring with:)
		AC_MSG_WARN(\"$0 --build=$build --host=avr\")
		AC_MSG_WARN()
		AC_MSG_ERROR(aborting configure)
		;;
esac

## TODO: Write a check for GNU Make

dnl The default check whether the C compiler can create an executable
dnl is inappropriate for us as it requires an existing library.
AC_NO_EXECUTABLES
AM_INIT_AUTOMAKE

# We don't want touse the cflags from the environment since we need control
# of this when we're building the libs.
CFLAGS=""

# Checks for programs.

AC_PROG_CC
AC_CHECK_TOOL(AS, as, as)
AM_PROG_AS
AC_PROG_RANLIB
AC_CHECK_TOOL(AR, ar, ar)

# Make sure that we found the right avr cross-compiler.

case "${CC}" in
   *avr-gcc*) ;;
   *) AC_MSG_ERROR(Wrong C compiler found; check the PATH!) ;;
esac
case "${AS}" in
   *avr-as*) ;;
   *) AC_MSG_ERROR(Wrong assembler found; check the PATH!) ;;
esac
case "${AR}" in
   *avr-ar*) ;;
   *) AC_MSG_ERROR(Wrong archiver found; check the PATH!) ;;
esac
case "${RANLIB}" in
   *avr-ranlib*) ;;
   *) AC_MSG_ERROR(Wrong ranlib found; check the PATH!) ;;
esac

AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

dnl TODO: Check for various tools used to build the documentation. If anything
dnl is missing, don't build the docs by default.

dnl Disable building of dox by default since it they may not build properly on
dnl all systems. This isn't optimal, but how do we work around the user not
dnl having fig2dev and still build all the formats?

AC_ARG_ENABLE(doc,
[  --enable-doc            build all doc formats (disabled is default)],
[case "${enableval}" in
  yes) enable_doc=yes ;;
  no)  enable_doc=no ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for global doc option) ;;
 esac], [enable_doc=no])dnl

dnl Various documentation options.  By default, we enable all of them,
dnl i. e. HTML, Postscript, PDF, and Unix-style man pages.
AC_ARG_ENABLE(html-doc,
[  --enable-html-doc       build HTML documentation (default)],
[case "${enableval}" in
  yes) html_doc=yes ;;
  no)  html_doc=no ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for html-doc option) ;;
 esac], [html_doc=yes])dnl

AC_ARG_ENABLE(ps-doc,
[  --enable-ps-doc         build Postscript documentation (default)],
[case "${enableval}" in
  yes) ps_doc=yes ;;
  no)  ps_doc=no ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for ps-doc option) ;;
 esac], [ps_doc=yes])dnl

AC_ARG_ENABLE(pdf-doc,
[  --enable-pdf-doc        build PDF documentation (default)],
[case "${enableval}" in
  yes) pdf_doc=yes ;;
  no)  pdf_doc=no ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for pdf-doc option) ;;
 esac], [pdf_doc=yes])dnl

dnl Man pages are currently not tunable; they'll be built whenever
dnl the HTML documentation is built.  However, it can be configured
dnl whether the result is about to be installed or not.
AC_ARG_ENABLE(man-doc,
[  --enable-man-doc        build Unix-style manual pages (default)],
[case "${enableval}" in
  yes) man_doc=yes ;;
  no)  man_doc=no ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for man-doc option) ;;
 esac], [man_doc=yes])dnl

AC_ARG_ENABLE(versioned-doc,
[  --enable-versioned-doc  install docs in directory with version name (default)],
[case "${enableval}" in
  yes) versioned_doc=yes ;;
  no)  versioned_doc=no ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for versioned-doc option) ;;
 esac], [versioned_doc=yes])dnl

dnl troth/2002-07-12: The CHECK_DOXYGEN macro should be moved to a config
dnl dir. I put it here until I figure out how to integrate the config dir.

dnl
dnl @synopsis CHECK_DOXYGEN
dnl
dnl This macro checks if doxygen is installed on the build system
dnl
dnl @version Id: check_doxygen.m4,v 1.2 2002/02/10 23:22:40 troth Exp
dnl @author Theodore A. Roth <troth@openavr.org>
dnl
AC_DEFUN(CHECK_DOXYGEN,[dnl
dnl
AC_MSG_CHECKING([for doxygen])
dox_ver=`doxygen --version 2>/dev/null`
if test "x$dox_ver" = "x"; then
	AC_MSG_RESULT(no)
else
	# FIXME: should also check for dox_ver >= 1.4.1
	AC_MSG_RESULT(yes)
	if test "$ps_doc" = "yes"; then
		AC_MSG_NOTICE([Enabling PS docs])
		TARGET_DOX_PS=dox-ps
		INSTALL_DOX_PS=install-dox-ps
	fi
	if test "$pdf_doc" = "yes"; then
		AC_MSG_NOTICE([Enabling PDF docs])
		TARGET_DOX_PDF=dox-pdf
		INSTALL_DOX_PDF=install-dox-pdf
	fi
	if test "$html_doc" = "yes"; then
		AC_MSG_NOTICE([Enabling HTML docs])
		TARGET_DOX_HTML=dox-html
		INSTALL_DOX_HTML=install-dox-html
	fi
dnl The creation of man pages is a side-effect of the HTML
dnl generation, so a different target is only needed for installation.
	if test "$man_doc" = "yes"; then
		AC_MSG_NOTICE([Enabling man pages])
		TARGET_DOX_HTML=dox-html
		INSTALL_DOX_MAN=install-dox-man
	fi
fi
dnl
AC_SUBST(TARGET_DOX_PS)
AC_SUBST(TARGET_DOX_PDF)
AC_SUBST(TARGET_DOX_HTML)
AC_SUBST(INSTALL_DOX_PS)
AC_SUBST(INSTALL_DOX_PDF)
AC_SUBST(INSTALL_DOX_HTML)
AC_SUBST(INSTALL_DOX_MAN)
])dnl

CHECK_DOXYGEN

dnl
dnl @synopsis CHECK_PNG_UTILS
dnl
dnl This macro checks if pngtopnm and pnmtopng are installed. If they are not
dnl installed we just fake it with "cat".
dnl
dnl We use these in the dox to insert transparency into the png images as such:
dnl
dnl   $ pngtopnm foo.png > tmp.pnm
dnl   $ pnmtopng -transparent white tmp.pnm > foo.png
dnl
dnl @version Id: check_doxygen.m4,v 1.2 2002/02/10 23:22:40 troth Exp
dnl @author Theodore A. Roth <troth@openavr.org>
dnl
AC_DEFUN(CHECK_PNG_UTILS,[dnl
dnl
AC_MSG_CHECKING([for pngtopnm])
has_pngtopnm=`pngtopnm --version 2>&1 | grep -c Version`
if test "$has_pngtopnm" = "1"; then
	AC_MSG_RESULT(yes)
	PNGTOPNM="pngtopnm"
else
	AC_MSG_RESULT(no)
	PNGTOPNM="cat"
fi
dnl
AC_MSG_CHECKING([for pngtopnm])
has_pnmtopng=`pnmtopng --version 2>&1 | grep -c Version`
if test "$has_pnmtopng" = "1"; then
	AC_MSG_RESULT(yes)
	PNMTOPNG="pnmtopng"
else
	AC_MSG_RESULT(no)
	PNMTOPNG="cat"
fi
dnl
dnl pnmtopng will fail if the input file is a png instead of a pnm.
dnl
if test "$PNMTOPNG" = "cat"; then
	PNGTOPNM="cat"
fi
dnl
AC_SUBST(PNGTOPNM)
AC_SUBST(PNMTOPNG)
])dnl

CHECK_PNG_UTILS

if test "$versioned_doc" = "yes"; then
	DOC_INST_DIR='${DESTDIR}${datadir}/doc/avr-libc-$(VERSION)'
	AVR_LIBC_USER_MANUAL="avr-libc-user-manual-${VERSION}"
else
	DOC_INST_DIR='${DESTDIR}${datadir}/doc/avr-libc'
	AVR_LIBC_USER_MANUAL="avr-libc-user-manual"
fi

dnl We only want to build the docs if the user has passed
dnl "--enable-doc" to configure.
if test "$enable_doc" = "yes"; then
	DOCSDIR='doc'
else
	DOCSDIR=''
fi

AC_SUBST(DOCSDIR)
AC_SUBST(AVR_LIBC_USER_MANUAL)
AC_SUBST(DOC_INST_DIR)

dnl Some devices are only handled by newer version of gcc. This macro lets us
dnl probe to see if the installed avr-gcc supports a questionable device.
AC_DEFUN(
  CHECK_AVR_DEVICE,
  [
    old_CC=${CC}
    old_CFLAGS=${CFLAGS}
    CFLAGS="-mmcu=$1"
    CC=`echo "${CC}" | sed 's/-mmcu=avr.//'`
    AC_MSG_CHECKING(if ${CC} has support for $1)
    AC_TRY_COMPILE(
      [],
      [],
      [has_dev_support=yes],
      [has_dev_support=no]
    )
    if test "x$has_dev_support" = "xyes"
    then
      HAS_$1=yes
    fi
    AC_MSG_RESULT($has_dev_support)
    CC=${old_CC}
    CFLAGS=${old_CFLAGS}
  ]
)
dnl For those directories which are not supported by this version of gcc,
dnl drop a dummy Makefile so the hierarchy traversal will work.
AC_DEFUN(
  DUMMY_MAKEFILE,
  [
    AC_MSG_NOTICE([Creating dummy $1])
    echo all distdir install installdirs clean distclean uninstall check: > $1
  ]
)

# Generate all files from *.in sources.

AC_CONFIG_FILES([
	Makefile
	avr-libc.spec
	avr/Makefile
	avr/lib/Makefile
	common/Makefile
	crt1/Makefile
	doc/Makefile
	doc/api/Makefile
	doc/examples/Makefile
	include/Makefile
	include/avr/Makefile
	include/compat/Makefile
	libc/Makefile
	libc/misc/Makefile
	libc/pmstring/Makefile
	libc/stdio/Makefile
	libc/stdlib/Makefile
	libc/string/Makefile
	libm/Makefile
	libm/fplib/Makefile
	scripts/Makefile
])

#avr1 and avr2
AC_CONFIG_FILES([
	avr/lib/avr2/Makefile
	avr/lib/avr2/at90s1200/Makefile
	avr/lib/avr2/at90s2313/Makefile
	avr/lib/avr2/at90s2323/Makefile
	avr/lib/avr2/at90s2333/Makefile
	avr/lib/avr2/at90s2343/Makefile
	avr/lib/avr2/at90s4433/Makefile
	avr/lib/avr2/at90s4414/Makefile
	avr/lib/avr2/at90s4434/Makefile
	avr/lib/avr2/at90s8515/Makefile
	avr/lib/avr2/at90s8535/Makefile
	avr/lib/avr2/at90c8534/Makefile
	avr/lib/avr2/attiny11/Makefile
	avr/lib/avr2/attiny12/Makefile
	avr/lib/avr2/attiny15/Makefile
	avr/lib/avr2/attiny22/Makefile
	avr/lib/avr2/attiny26/Makefile
	avr/lib/avr2/attiny28/Makefile
	avr/lib/avr2/at86rf401/Makefile
])

CHECK_AVR_DEVICE(attiny13)
AS_IF([test "x$HAS_attiny13" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr2/attiny13/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr2/attiny13/Makefile])])

CHECK_AVR_DEVICE(attiny2313)
AS_IF([test "x$HAS_attiny2313" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr2/attiny2313/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr2/attiny2313/Makefile])])

CHECK_AVR_DEVICE(attiny25)
AS_IF([test "x$HAS_attiny25" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr2/attiny25/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr2/attiny25/Makefile])])

CHECK_AVR_DEVICE(attiny45)
AS_IF([test "x$HAS_attiny45" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr2/attiny45/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr2/attiny45/Makefile])])

CHECK_AVR_DEVICE(attiny85)
AS_IF([test "x$HAS_attiny85" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr2/attiny85/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr2/attiny85/Makefile])])

#avr3
AC_CONFIG_FILES([
	avr/lib/avr3/Makefile
	avr/lib/avr3/atmega103/Makefile
	avr/lib/avr3/at43usb320/Makefile
	avr/lib/avr3/at43usb355/Makefile
	avr/lib/avr3/at76c711/Makefile
])

#avr4
AC_CONFIG_FILES([
	avr/lib/avr4/Makefile
	avr/lib/avr4/atmega8/Makefile
	avr/lib/avr4/atmega8515/Makefile
	avr/lib/avr4/atmega8535/Makefile
])

CHECK_AVR_DEVICE(atmega48)
AS_IF([test "x$HAS_atmega48" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr4/atmega48/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr4/atmega48/Makefile])])

CHECK_AVR_DEVICE(atmega88)
AS_IF([test "x$HAS_atmega88" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr4/atmega88/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr4/atmega88/Makefile])])

CHECK_AVR_DEVICE(at90pwm2)
AS_IF([test "x$HAS_at90pwm2" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr4/at90pwm2/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr4/at90pwm2/Makefile])])

CHECK_AVR_DEVICE(at90pwm3)
AS_IF([test "x$HAS_at90pwm3" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr4/at90pwm3/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr4/at90pwm3/Makefile])])

#avr5
AC_CONFIG_FILES([
	avr/lib/avr5/Makefile
	avr/lib/avr5/atmega16/Makefile
	avr/lib/avr5/atmega161/Makefile
	avr/lib/avr5/atmega162/Makefile
	avr/lib/avr5/atmega163/Makefile
	avr/lib/avr5/atmega169/Makefile
	avr/lib/avr5/atmega32/Makefile
	avr/lib/avr5/atmega323/Makefile
	avr/lib/avr5/atmega64/Makefile
	avr/lib/avr5/atmega128/Makefile
	avr/lib/avr5/at94k/Makefile
])

CHECK_AVR_DEVICE(atmega164)
AS_IF([test "x$HAS_atmega164" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr5/atmega164/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr5/atmega164/Makefile])])

CHECK_AVR_DEVICE(atmega165)
AS_IF([test "x$HAS_atmega165" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr5/atmega165/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr5/atmega165/Makefile])])

CHECK_AVR_DEVICE(atmega168)
AS_IF([test "x$HAS_atmega168" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr5/atmega168/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr5/atmega168/Makefile])])

CHECK_AVR_DEVICE(atmega324)
AS_IF([test "x$HAS_atmega324" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr5/atmega324/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr5/atmega324/Makefile])])

CHECK_AVR_DEVICE(atmega325)
AS_IF([test "x$HAS_atmega325" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr5/atmega325/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr5/atmega325/Makefile])])

CHECK_AVR_DEVICE(atmega3250)
AS_IF([test "x$HAS_atmega3250" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr5/atmega3250/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr5/atmega3250/Makefile])])

CHECK_AVR_DEVICE(atmega329)
AS_IF([test "x$HAS_atmega329" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr5/atmega329/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr5/atmega329/Makefile])])

CHECK_AVR_DEVICE(atmega3290)
AS_IF([test "x$HAS_atmega3290" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr5/atmega3290/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr5/atmega3290/Makefile])])

CHECK_AVR_DEVICE(atmega644)
AS_IF([test "x$HAS_atmega644" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr5/atmega644/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr5/atmega644/Makefile])])

CHECK_AVR_DEVICE(atmega645)
AS_IF([test "x$HAS_atmega645" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr5/atmega645/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr5/atmega645/Makefile])])

CHECK_AVR_DEVICE(atmega6450)
AS_IF([test "x$HAS_atmega6450" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr5/atmega6450/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr5/atmega6450/Makefile])])

CHECK_AVR_DEVICE(atmega649)
AS_IF([test "x$HAS_atmega649" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr5/atmega649/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr5/atmega649/Makefile])])

CHECK_AVR_DEVICE(atmega6490)
AS_IF([test "x$HAS_atmega6490" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr5/atmega6490/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr5/atmega6490/Makefile])])

CHECK_AVR_DEVICE(at90can128)
AS_IF([test "x$HAS_at90can128" = "xyes"],
      [AC_CONFIG_FILES([avr/lib/avr5/at90can128/Makefile])],
      [DUMMY_MAKEFILE([avr/lib/avr5/at90can128/Makefile])])

AC_OUTPUT
